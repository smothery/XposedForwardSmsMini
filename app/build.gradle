apply plugin: 'com.android.application'

def keyFile = rootProject.file("keystore.jks")

def static releaseTime() {
    return new Date().format("yyMMdd", TimeZone.default)
}

def VERSION_NAME = "0.0.1"
def VERSION_CODE = 1

android {
    namespace 'com.xmhzj.xposed.forwardSmsMini'

    compileSdk 34
    defaultConfig {
        applicationId "com.xmhzj.xposed.forwardSmsMini"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField("String", "LOG_TAG", "\"xposedForwardSmsMini\"")
        buildConfigField("int", "MODULE_VERSION", "" + VERSION_CODE)
        resConfigs 'en', 'zh-rCN', 'zh-rTW'
    }

    signingConfigs {
        release {
            storeFile keyFile
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            buildConfigField("int", "LOG_LEVEL", "2")
            buildConfigField("boolean", "LOG_TO_XPOSED", "true")
            buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")

            if (keyFile.exists()) {
                signingConfig signingConfigs.release
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            buildConfigField("int", "LOG_LEVEL", "4")
            buildConfigField("boolean", "LOG_TO_XPOSED", "true")
            buildConfigField("boolean", "LOG_TO_EDXPOSED", "true")
            if (keyFile.exists()) {
                signingConfig signingConfigs.release
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            lintOptions {
                disable 'MissingTranslation'
                checkReleaseBuilds false
                abortOnError false
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "xposedForwardSmsMini_v${variant.versionName.replaceAll('\\s+', '_')}_${releaseTime()}_${variant.buildType.name.charAt(0)}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

ext {
    andriodShellVersion = "1.0.0" // Android Shell

    gsonVersion = "2.9.0" // gson

    okHttpVersion = "4.4.0" // OkHttp
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Xposed
    compileOnly 'de.robv.android.xposed:api:82'

    // Android Shell
    implementation "com.jaredrummler:android-shell:$andriodShellVersion"

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
}
